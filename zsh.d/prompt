autoload -Uz vcs_info
autoload -Uz add-zsh-hook
autoload -Uz colors; colors

setopt prompt_subst
setopt prompt_percent

zstyle ':vcs_info:*' enable git hg
zstyle ':vcs_info:*' get-revision true

zstyle ':vcs_info:*' formats '[%b]-[%10>...>%i%>>]'
zstyle ':vcs_info:*' actionformats '[%b|%a]-[%10>...>%i%>>]'

function _precure() {
  vcs_info

  local symbols='-@  <>[]-'
  local user=%n
  local host=%m
  local date='%D{%Y/%m/%d %H:%M}'
  local code='%(?.:%).:()'

  local length=$((${#${(%)user}}
        + ${#${(%)host}}
        + ${#${(%)date}}
        + ${#${(%)code}}))

  local c_wd="%$((${COLUMNS} - $length - ${#symbols} - 1))<...<%~"

  host=$fg[yellow]$host$reset_color
  code="%(?.${fg[green]}.${fg[red]})"$code$reset_color

  local upper_left="-${user}@${host} ${code} <${date}>"
  local upper_right="[${(%)c_wd}]-"


  local lower='-%# '

  local line=${(l.(${COLUMNS} - $length - ${#${(%)c_wd}} - ${#symbols})..-.)}

  PROMPT=$upper_left$line$upper_right$'\n'$lower
  RPS1=$vcs_info_msg_0_
}

add-zsh-hook precmd _precure

